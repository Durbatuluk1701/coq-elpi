% returns the classes on which the current gref depends
pred get-sub-classes i:gref, o:list gref.
get-sub-classes GR Res :-
  coq.env.dependencies GR _ DepSet,
  coq.gref.set.elements DepSet DepList,
  std.filter DepList coq.TC.class? Res.

% returns all the instances of the passed ClassName
pred get-inst-by-tc-name i:gref, o:list gref.
get-inst-by-tc-name TC GRL:-
  coq.TC.db-for TC Inst,
  std.map Inst inst->gref GRL.

% takes a tc-instance and return the gref
pred inst->gref i:tc-instance, o:gref.
inst->gref (tc-instance Res _) Res.

pred has-class i:term.
has-class (app [global T|_]) :- coq.TC.class? T. 

pred context-dep i:term, o:list constant.
context-dep Inst DepInter :-
  global A = Inst,
  coq.env.dependencies A _ Dep,
  std.filter {coq.env.section} (elt\ coq.gref.set.mem (const elt) Dep) DepInter.

pred compile-no-context i:term, i:term, i:list prop, i:list term, o:prop.
compile-no-context (prod _ T F) I ListRHS ListVar (pi x\ C x) :-
  pi p\ sigma L\
    if (has-class T) (L = [tc T p | ListRHS]) (L = ListRHS),
    compile-no-context (F p) I L [p | ListVar] (C p).
compile-no-context Ty I ListRHS ListVar Clause :- 
  std.rev ListRHS RevRHS,
  AppInst = app [I | {std.rev ListVar}],
  Term = (tc Ty AppInst :- RevRHS),
  Term = Clause
  .

pred debug-add-clause i:A, i:B.
:if "debug-add-clause"
debug-add-clause Clause Ty :- 
  coq.say "The new clause is" Clause "with type:" Ty, fail.
debug-add-clause _ _.

pred make-clause i:gref, o:clause.
make-clause Inst Clause :-
  coq.env.typeof Inst Ty,
  compile-no-context Ty (global Inst) [] [] C,
  Clause = clause _ (before "complexHook") C,
  debug-add-clause Clause Ty.

pred debug-add-instance i:A.
:if "add instance"
debug-add-instance Inst :- coq.say "Adding instance:" Inst, fail.
debug-add-instance _.

pred add-inst-to-queued i:gref.
add-inst-to-queued Inst :-
  const InstConst = Inst,
  if ({std.length {context-dep (global Inst)}} > 0, not (std.mem {coq.env.section} InstConst)) 
      (@local! => coq.elpi.accumulate _ "tc.db" (clause _ _ (queued-instances Inst))) true.

% [add-inst->db IgnoreClassDepL Inst] add the Inst to
% the database except those depending on at least one 
% inside IgnoreClassDepL
pred add-inst->db i:list gref, i:gref.
add-inst->db IgnoreClassDepL Inst :-
  get-sub-classes Inst Dep,
  if (not (instance Inst), not (std.exists Dep (x\ std.mem IgnoreClassDepL x))) 
  (
    add-inst-to-queued Inst,
    if ({std.length Dep} > 1) (
      coq.warning "TC-warning" "add-inst-with-multiple-deps" 
      "Adding" Inst "which dependes on mulitple class dependencies:" Dep) 
      true,
    coq.elpi.accumulate _ "tc.db" {make-clause Inst},
    coq.elpi.accumulate _ "tc.db" (clause _ _ (instance Inst)),
    debug-add-instance Inst
  ) 
  true.

pred add-tc i:list gref, i:gref.
add-tc IgnoreDepClassGR GR:-
  add-modes GR, 
  get-inst-by-tc-name GR InstL,
  std.forall InstL (add-inst->db IgnoreDepClassGR).

pred is-instance i:gref.
is-instance GR :-
  coq.env.typeof GR (app [global TC | _]),
  coq.TC.class? TC,
  add-modes TC.

% [add-tc-or-inst IgnoreDepClass ClassName] look
% for all the instances of ClassName and call the pred
% add-inst->db
pred add-tc-or-inst i:list string, i:list string.
add-tc-or-inst IgnoreDepClass Names :-
  std.map IgnoreDepClass coq.locate IgnoreDepClassGR,
  std.forall Names (n\ sigma GR\
    coq.locate n GR,
    if2 (coq.TC.class? GR) 
      (add-tc IgnoreDepClassGR GR)
      (is-instance GR)(add-inst->db IgnoreDepClassGR GR)
      (coq.warning "not-inst-tc" "not-inst-tc" n "is neither a TC nor a instance")
  ).

% takes a Path and a GR and returns if 
% the GR is located in Path
pred is-in-path i:string, i:gref. 
is-in-path Path GR :- 
  std.mem {coq.gref->path GR} Path.

% Look for the instances of ClassName 
% that are located in Path.
pred add-path i:string, i:string.
add-path ClassName Path :-
  coq.locate ClassName GR,
  std.filter {get-inst-by-tc-name GR} (is-in-path Path) InstInPath,
  std.forall InstInPath (add-inst->db []).